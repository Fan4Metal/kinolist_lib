; Script generated by the Inno Setup Script Wizard.

#define MyAppName "Kinolist_Lib"
#define MyAppVersion "0.2.40"
#define VersionInfoVersion "0.1.0.0"
#define MyAppPublisher "Vanyunin Alexander"

[Setup]
AppId={{F0838CE3-15E5-4782-9495-B6E3DA8718CA}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
VersionInfoVersion={#VersionInfoVersion}
AppCopyright=Copyright (C) 2022-2025 {#MyAppPublisher}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={userappdata}\{#MyAppName}
DefaultGroupName={#MyAppName}
PrivilegesRequired=lowest
OutputDir=.\dist\
SetupIconFile=.\icon.ico
UninstallDisplayIcon=.\icon.ico
LicenseFile=.\LICENSE
OutputBaseFilename=Kinolist_Lib {#MyAppVersion} Setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ChangesEnvironment=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Files]
Source: ".\dist\kl\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
; set PATH
Root: HKCU; Subkey: "Environment"; ValueType:string; ValueName:"Path"; ValueData:"{olddata};{app}"; Flags: preservestringtype; Check: NeedsAddPath(ExpandConstant('{app}'))
Root: HKA; Subkey: "Software\Classes\Directory\Background\shell\Kinolist_lib"; Flags: uninsdeletekey 
Root: HKA; Subkey: "Software\Classes\Directory\Background\shell\Kinolist_lib"; ValueName: "MUIVerb"; ValueType: String; ValueData: "Список по тегам"; Flags: uninsdeletevalue 
Root: HKA; Subkey: "Software\Classes\Directory\Background\shell\Kinolist_lib"; ValueName: "Icon"; ValueType: ExpandSZ; ValueData: "{userappdata}\Kinolist_Lib\kl.exe"; Flags: uninsdeletevalue 
Root: HKA; Subkey: "Software\Classes\Directory\Background\shell\Kinolist_lib\command"; Flags: uninsdeletekey 
Root: HKA; Subkey: "Software\Classes\Directory\Background\shell\Kinolist_lib\command"; ValueType: ExpandSZ; ValueData: """{userappdata}\Kinolist_Lib\kl.exe"" --genres --loc ""%V"""; Flags: uninsdeletevalue 
Root: HKA; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Kinolist_Lib"; Flags: uninsdeletekey 
Root: HKA; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Kinolist_Lib"; ValueType: String; ValueData: "Записать теги"; Flags: uninsdeletevalue 
Root: HKA; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Kinolist_Lib"; ValueName: "Icon"; ValueType: String; ValueData: "{userappdata}\Kinolist_Lib\kl.exe"; Flags: uninsdeletevalue 
Root: HKA; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Kinolist_Lib\command"; Flags: uninsdeletekey 
Root: HKA; Subkey: "Software\Classes\SystemFileAssociations\.mp4\shell\Kinolist_Lib\command"; ValueType: String; ValueData: """{userappdata}\Kinolist_Lib\kl.exe"" -t ""%1"""; Flags: uninsdeletevalue 

[Code]

function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_CURRENT_USER,
    'Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  { look for the path with leading and trailing semicolon }
  { Pos() returns 0 if not found }
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;

procedure RemovePath(Path: string);
var
  Paths: string;
  P: Integer;
begin
  if not RegQueryStringValue(HKEY_CURRENT_USER, 'Environment', 'Path', Paths) then
  begin
    Log('PATH not found');
  end
    else
  begin
    Log(Format('PATH is [%s]', [Paths]));

    P := Pos(';' + Uppercase(Path) + ';', ';' + Uppercase(Paths) + ';');
    if P = 0 then
    begin
      Log(Format('Path [%s] not found in PATH', [Path]));
    end
      else
    begin
      if P > 1 then P := P - 1;
      Delete(Paths, P, Length(Path) + 1);
      Log(Format('Path [%s] removed from PATH => [%s]', [Path, Paths]));

      if RegWriteStringValue(HKEY_CURRENT_USER, 'Environment', 'Path', Paths) then
      begin
        Log('PATH written');
      end
        else
      begin
        Log('Error writing PATH');
      end;
    end;
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
  begin
    RemovePath(ExpandConstant('{app}'));
  end;
end;
